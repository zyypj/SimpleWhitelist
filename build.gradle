plugins {
    id 'java'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

group = 'com.github.zyypj'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigot-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.36'

    implementation "co.aikar:acf-bukkit:0.5.1-SNAPSHOT"

    annotationProcessor 'org.projectlombok:lombok:1.18.36'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'co.aikar.commands', 'com.github.zyypj.simplewhitelist.relocate.acf'
    relocate 'co.aikar.locales', 'com.github.zyypj.simplewhitelist.relocate.locales'
}

build.dependsOn shadowJar